== Control panel ==
```
match
  [#session-connect]
commit
  [#div sort: 3 class: "controls"
    children: [#input
      sort: 1
      value: "focus here to move"
      style: [display: "block" margin: "1em 0"]]
    children: [#textarea
      sort: 2
      name: "level"
      style: [margin-right: "1em"]]
    children: [#button
      sort: 3
      name: "load"
      text: "Load/reset"
      style: [vertical-align: "bottom" margin-top: "1em"]]]
```
== Map loading ==
```
match
  [#click element: [#button name: "load"]]
  [#textarea name: "level" value: level]
  split[text: level, by: "\n" index: y token: line]
  split[text: line, by: "" index: x token: char]
  tag = if char = "@" then "player"
        else if char = "#" then "wall"
        else if char = "$" then "box"
        else if char = "." then "goal"
        else "cell"
commit
  [tag x y]
  [#cell x y]
```
== Decoding keypresses ==
Note that is quite reusable
```
match
  [#session-connect]
  left = 37
  up = 38
  right = 39
  down = 40
commit
  [#move-templ keycode: left  y-change:  0 x-change: -1]
  [#move-templ keycode: up    y-change: -1 x-change:  0]
  [#move-templ keycode: right y-change:  0 x-change:  1]
  [#move-templ keycode: down  y-change:  1 x-change:  0]
```
```
match
  [#move-templ keycode y-change x-change]
  [#keydown key: keycode]
bind
  [#move x-change y-change]
```
== Initialise counter ==
```
match
  [#session-connect]
commit
  [#counter moves: 0 pushes: 0]
```
== Game logic ==
Walking into an empty space
```
match
  not([#won])
  [#move x-change y-change]
  player = [#player x y]
  new-x = x + x-change
  new-y = y + y-change
  not([#box x: new-x y: new-y])
  not([#wall x: new-x y: new-y])
  counter = [#counter]
commit
  player.x := new-x
  player.y := new-y
  counter.moves := counter.moves + 1
```
Pushing a box
```
match
  not([#won])
  [#move y-change x-change]
  player = [#player x y]
  new-x = x + x-change
  new-y = y + y-change
  box = [#box x: new-x y: new-y]
  new-box-x = new-x + x-change
  new-box-y = new-y + y-change
  not([#box x: new-box-x y: new-box-y])
  not([#wall x: new-box-x y: new-box-y])
  counter = [#counter]
commit
  player.x := new-x
  player.y := new-y
  box.x := new-box-x
  box.y := new-box-y
  counter.pushes := counter.pushes + 1
```
Win condition
```
match
  all-boxes = [#box]
  [#goal x y]
  goal-boxes = [#box x y]
  count[given: all-boxes] = count[given: goal-boxes]
bind
  [#won]
```
== Drawing ==
Tile definitions
```
match
  [#session-connect]
commit
  [@tiles player: "ðŸ‘·" box: "ðŸ“¦" wall: "â¬›" goal:"ðŸ•³"]
```
Draw the board
```
match
  [@tiles player box wall goal]
  [#cell x y]
  tile = if [#player x y] then player
         else if [#wall x y] then wall
         else if [#box x y] then box
         else if [#goal x y] then goal
         else ""
  color = if [#goal x y] then "cyan"
          else "white"
  win-text = if [#won] then "YOU WIN!"
             else ""
  counter = [#counter moves pushes]
bind
  [#div sort: 1 style:
    [text-align: "center"
     position: "relative"]
    children:
      [#div sort: 1 style: [position: "absolute"
                            left: "50%"
                            top: "50%"
                            font-size: "5em"
                            transform: "translateX(-50%) translateY(-50%)"
                            white-space: "nowrap"
                            color: "black"
                            text-shadow: "white -1px 1px, white -2px 2px, white -3px 3px"] text: win-text]
    children:
      [#div sort: 2 style:
        [font-size: "24px"
         display: "inline-block"
         text-align: "left"]
        children:
          [#div sort: y children:
            [#div sort: x style: [color
                                  line-height: "24px"
                                  width: "24px"
                                  height: "24px"
                                  display: "inline-block"
                                  vertical-align: "bottom"
                                  text-align: "center"]
             text: tile]]]]
  [#div sort: 2 text: "Moves: {{moves}} Pushes: {{pushes}}"]
```
TODO:
 * Wall clock timer
 * Refactor to use 2d vector add
 * Style sheet
 * Resetting
 * Support maps with player or box on goal
