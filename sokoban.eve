## Draw control panel
```
commit @browser
  [#div sort: 3 class: "controls"
    style: [margin: "1em 0"]
    children: [#input
      sort: 1
      value: "focus here to move"
      style: [display: "block" margin: "1em 0"]]
    children: [#textarea
      sort: 2
      name: "level"
      style: [margin-right: "1em"]]
    children: [#button
      sort: 3
      name: "load"
      text: "Load/reset"
      style: [vertical-align: "bottom" margin-top: "1em"]]
    children: [#label
      text: "ASCII mode"
      children: [#input
        type: "radio"
        name: "gfx-mode"
        value: "ascii"]]
    children: [#label
      text: "Unicode mode"
      children: [#input
        type: "radio"
        name: "gfx-mode"
        value: "unicode"
        checked: true]]]
```
## Map loading and resetting
### Bind load button click to do reset
```
search @browser @event
  [#click element: [#button name: "load"]]
bind @event
  [#do-reset]
```
### Bind clicking on won game to do reset
```
search @browser @event
  [#click]
  [#game-state state: "won"]
bind @event
  [#do-reset]
```
### Remove previous map/state
```
search @event @session
  [#do-reset]
  old = if [#cell] then [#cell]
        if [#player] then [#player]
        if [#wall] then [#wall]
        if [#box] then [#box]
        if [#goal] then [#goal]
        if [#counter] then [#counter]
        if [#game-state] then [#game-state]
commit
  old := none
```
### Add new counter and state
```
search @event @session
  [#do-reset]
commit
  [#counter moves: 0 pushes: 0]
  [#game-state state: "playing"]
```
### Load map from textarea
```
search @session @browser @event
  [#do-reset]
  [#textarea name: "level" value: level]
  split[text: level, by: "\n" index: y token: line]
  split[text: line, by: "" index: x token: char]
  tag = if char = "@" then "player"
        else if char = "#" then "wall"
        else if char = "$" then "box"
        else if char = "." then "goal"
        else "cell"
commit
  [tag x y]
  [#cell x y]
```
## Keypress decoding
Note that is quite reusable
### Define keypress decoding data
```
commit
  [#move-templ keycode: 37 y-change:  0 x-change: -1]
  [#move-templ keycode: 38 y-change: -1 x-change:  0]
  [#move-templ keycode: 39 y-change:  0 x-change:  1]
  [#move-templ keycode: 40 y-change:  1 x-change:  0]
```
### Decode keypresses
```
search @session @event
  [#move-templ keycode y-change x-change]
  [#keydown key: keycode]
bind
  [#move x-change y-change]
```
## Game logic
### Walking into an empty space
```
search
  [#game-state state: "playing"]
  [#move x-change y-change]
  player = [#player x y]
  new-x = x + x-change
  new-y = y + y-change
  not([#box x: new-x y: new-y])
  not([#wall x: new-x y: new-y])
  counter = [#counter]
commit
  player.x := new-x
  player.y := new-y
  counter.moves := counter.moves + 1
```
### Pushing a box
```
search
  [#game-state state: "playing"]
  [#move y-change x-change]
  player = [#player x y]
  new-x = x + x-change
  new-y = y + y-change
  box = [#box x: new-x y: new-y]
  new-box-x = new-x + x-change
  new-box-y = new-y + y-change
  not([#box x: new-box-x y: new-box-y])
  not([#wall x: new-box-x y: new-box-y])
  counter = [#counter]
commit
  player.x := new-x
  player.y := new-y
  box.x := new-box-x
  box.y := new-box-y
  counter.pushes := counter.pushes + 1
```
### Win condition
```
search
  game-state = [#game-state]
  all-boxes = [#box]
  [#goal x y]
  goal-boxes = [#box x y]
  count[given: all-boxes] = count[given: goal-boxes]
bind
  game-state.state := "won"
```
## Drawing
### Unicode tile definitions
```
search @browser
  [#input name: "gfx-mode" value: "unicode" checked]
bind
  [#tiles player: "ðŸ‘·" box: "ðŸ“¦" wall: "â¬›" goal: "ðŸ•³"]
```
### Ascii tile definitions
```
search @browser
  [#input name: "gfx-mode" value: "ascii" checked]
bind
  [#tiles player: "@" box: "$" wall: "#" goal: "."]
```
### Draw the board
```
search
  [#tiles player box wall goal]
  [#cell x y]
  tile = if [#player x y] then player
         else if [#wall x y] then wall
         else if [#box x y] then box
         else if [#goal x y] then goal
         else ""
  color = if [#goal x y] then "cyan"
          else "black"
  win-text = if [#game-state state: "won"] then "YOU WIN!"
             else ""
  counter = [#counter moves pushes]
bind @browser
  [#div sort: 1 style:
    [text-align: "center"
     position: "relative"]
    children:
      [#div sort: 1 style: [position: "absolute"
                            left: "50%"
                            top: "50%"
                            font-size: "3em"
                            transform: "translateX(-50%) translateY(-50%)"
                            white-space: "nowrap"
                            color: "white"
                            text-shadow: "black -1px 1px, black -2px 2px, black -3px 3px"] text: win-text]
    children:
      [#div sort: 2 style:
        [font-size: "24px"
         display: "inline-block"
         text-align: "left"]
        children:
          [#div sort: y children:
            [#div sort: x style: [color
                                  line-height: "24px"
                                  width: "24px"
                                  height: "24px"
                                  display: "inline-block"
                                  vertical-align: "bottom"
                                  text-align: "center"]
             text: tile]]]]
  [#div style: [margin: "1em 0"] sort: 2 text: "Moves: {{moves}} Pushes: {{pushes}}"]
```
## To do
 * Wall clock timer
 * Refactor to use 2d vector add
 * Style sheet
 * Resetting
 * Support maps with player or box on goal
